      SUBROUTINE CHKDE (ITYPC, IMC, IVCON, NC)                              CHKD
C
C* REVISED 25-JUL-89.
C  CHECKS CONSISTENCY FOR TRANSLATION INTO EXIR FORMAT.
C
C  ITYPC RECEIVES THE CHARACTER TYPES.
C  IMC RECEIVES THE CHARACTER MASK.
C  IVCON RECEIVES THE VALUE CONSTRAINTS.
C  NC RECEIVES THE NUMBER OF CHARACTERS.
C
      DIMENSION ITYPC(NC),IMC(NC),IVCON(NC)
C
      COMMON /INPXXX/ IBUF(121),JBUF,JBDAT,JEDAT,IDERR,NCERR,NSERR,NWERR
C
      DO 10 IC = 1, NC
        IF (ITYPC(IC).EQ.5)  IMC(IC) = 0
        IF (IMC(IC).NE.0 .AND. (ITYPC(IC).EQ.3.OR.ITYPC(IC).EQ.4)
     *   .AND. IVCON(IC).LE.0)  CALL MESSA (1, -3, IBUF)
   10   CONTINUE
      RETURN
C
      END
      SUBROUTINE CHKDIR (LD, JDIR, ICPH, W, IW, LW)                         CHKD
 
C* REVISED 15-JUN-99.
C* CARRIES OUT CHECKS BEFORE PROCESSING DIRECTIVE.
 
C  LD RECEIVES THE TYPE OF THE DIRECTIVE ABOUT TO BE PROCESSED.
C  JDIR RECEIVES THE DIRECTIVE-USE FLAGS FOR TYPE-LD DIRECTIVES.
C    JDIR(ICPH) RETURNS 1.
C  ICPH RECEIVES THE CONTROL-PHRASE NUMBER.
 
C  LDIR IS SET TO LD.
C  STORAGE ALLOCATION AND INITIALIZATION TAKES PLACE WHEN THE
C    APPROPRIATE DIRECTIVE LEVEL IS REACHED.
C  VARIOUS PARAMETERS ARE CHECKED FOR CONSISTENCY.
 
C  23/7/87. WEIGHTS AND MASKS SET TO 1 INSTEAD OF -1.
 
      DIMENSION JDIR(1000)
      DIMENSION W(LW),IW(LW)
 
      COMMON /ADRXXX/ IITYPC,IIMC,IIMI,IWTC,IWTI,INSTAT,IKSTAT,
     * INEWOR,INDEC,IICDES,ILCDES,IITMAD,IWW1C,IWW2C,IWW1I,IWW2I,
     * IWW1S,IWW2S,IIDAT,IIDAM,IIDAV,IIPARA,IIEMPF,ILINKC,IICHHD,
     * IITSHD,IISBND,IIVCON,IICDEP,ILSTDE,INEWFL,IIMPL1,IIMPL2,
     * IIMPFL,IISUMM,IICPTW,IICPTB,IITPTR,ILNAME,IINTRL,IMANDC,IRBND,
     * INBND,IITXPT,INORNG,IIOMOR,IIFILS,IIREPS,IIADDT,IIADDC,IIEMPT,
     * IIEMPC,IMINC,IMAXC,INUMRN,INAFLG,ISCALF,IISRTS,IICNOT,IICNGP,
     * IITYMK,IIFMT,IIOMPD,IIOMLO,IICIMG,IICIGP,IITIMG,IITREC,IPCERR,
     * IABERR,IIOMFC,IIALTC,IIADNM,IIADDL,IIEMNM,IIEMPL,IIOUSP,IICSYN,
     * IILANG,IICSTR,IITHD,IITHREC,IICDNAM,IITFIL,IITFREC,IIXHD,IIXHREC,
     * IIFNT,IIUSECC,IIFOFSET,IITLNK,IILREC,IITLNK2,IINAUTOCC,IIOSUBJ,
     * ILAST
      COMMON /DIMXXX/ NC,MI,MS,LIDAT,LCSTR
      COMMON /DIRXXX/ LDIR
      COMMON /INPXXX/ IBUF(121),JBUF,JBDAT,JEDAT,IDERR,NCERR,NSERR,NWERR
      COMMON /MISXXX/ IMPVAL,IPACH,IPCHN,IPUNC,IRAB,IKEYCH,ICMPRS,
     * ITUNC,ITINAP,ITIMPL,IUNVAR,LNKC,IDEPC,LICDEP,IPSUMM,ICHSEQ,
     * ITMSEQ,INSRVA,IFATT,ITINTR,ICMAND,IUNRNG,IOMTOR,NEWFLS,IRSEMI,
     * IOMSTC,IADDCH,IEMPCH,IVARUN,ISCALC,ICNOTE,ICIMGE,ITIMGE,MTIMGE,
     * NTIMGE,IOMNSC,IOMCMT,IOMLOW,IERRPC,IERRAB,ICHNSE,NUMST0,IUMEAN,
     * NADNAM,NEMNAM,NODELO,IOMUSP,ICKYIMG,ITKYIMG,ISUPIMG,ICTIMGE,
     * ICTNAME,IHTML,IRTF,MTHEAD,NTHEAD,ITMHDGS,LCDNAM,MTFILE,NTFILE,
     * ITMFILS,MXHEAD,NXHEAD,IDXHDGS,INSIMGFN,ICOFILE,IUCC,IOUNC,ITLINK,
     * MTLINK,NTLINK,NAUTOCC,IOUTSUBJ
      COMMON /TRNXXX/ IPCHAR,IPITEM,IPNAME,ITRAN
 
      IF (LD.EQ.LDIR)  GO TO 2000
      IF (LD.GT.LDIR)  GO TO 3000
 
C--   DIRECTIVE TYPE TOO LOW.
      CALL MESSA (2, -1, 0)
      GO TO 8000
 
C--   DIRECTIVE TYPE SAME AS CURRENT LEVEL.
 2000 IF (JDIR(ICPH).NE.0)  CALL MESSA (3, 1, 0)
      GO TO 8000
C
C--   DIRECTIVE TYPE HIGHER THAN CURRENT LEVEL.
 3000 IF (LDIR.GT.0.AND.NCERR.NE.0)  GO TO 9000
C
      I = LDIR + 1
      GO TO (3100,3200,3300,3400,3500), I
C
 3100 LDIR = 1
      IF (LDIR.GE.LD)  GO TO 8000
C
 3200 LDIR = 2
      CALL ALLOC1 (LW)
      CALL SETIA (IW, ILAST, 0)
      CALL SETIA (IW(IITYPC), NC, 1)
      DO 3210 IC = 1, NC
        J = INSTAT + IC - 1
        IW(J) = 2
        J = INEWOR + IC - 1
        IW(J) = IC
 3210   CONTINUE
      CALL SETIA (IW(IIMC), NC, 1)
      CALL SETIA (IW(IIMI), MI, 1)
      CALL COPIA (IW(INSTAT), IW(IKSTAT), NC)
      CALL SETIA (IW(INDEC), NC, -5)
      CALL SETRA (W(IWTC), NC, 1.)
      CALL SETRA (W(IWTI), MI, 1.)
      IF (LDIR.GE.LD)  GO TO 8000
C
 3300 LDIR = 3
      DO 3310 IC = 1, NC
        I = IITYPC + IC - 1
        J = INSTAT + IC - 1
        K = IKSTAT + IC - 1
        IW(J) = 2
        IF (IW(I).GE.3) IW(J) = 0
        IW(K) = 2
        IF (IW(I).GE.3)  IW(K) = 0
 3310   CONTINUE
      IF (LDIR.GE.LD)  GO TO 8000
C
 3400 LDIR = 4
      IF (LDIR.GE.LD)  GO TO 8000
C
 3500 LDIR = 5
      IF (ITRAN.EQ.4.AND.LNKC.NE.0) THEN
C       Allocate space to save feature offsets for LINKED characters in natural
C       language output. The values are calculated in NATTI.
        CALL ALLOC2 (LW, IIFOFSET, NC)
      ENDIF

      IF (IPSUMM.EQ.0)  GO TO 3530
C
C     Allocate space for SUMMARY statistics.
      N = NC
      DO 3520 IC = 1, NC
        JTYPC = IABS(IW(IITYPC+IC-1))
        IF (JTYPC.EQ.5)  GO TO 3515
        IF (JTYPC.GE.3)  GO TO 3510
C
C-      MULTISTATE.
        N = N + 4 + IW(INSTAT+IC-1)
        IF (IW(IISBND+IC-1).NE.0)  N = N + IW(IKSTAT+IC-1) + 1
        GO TO 3520
C
C-      NUMERIC.
 3510   N = N + 13
        IF (IW(IISBND+IC-1).NE.0)  N = N + IW(IKSTAT+IC-1) + 1
        GO TO 3520
C
C       TEXT.
 3515   N = N + 1
C
 3520   CONTINUE
C
      CALL ALLOC2 (LW, IISUMM, N)
      CALL INISUM (IW(IISUMM), IW(IISBND), LIDAT, IW(INSTAT),
     * IW(IKSTAT), IW(IITYPC), NC)
C
 3530 CALL ALLOC2 (LW, IICSTR, LIDAT)
      LCSTR = LW - IICSTR + 1
 
C
C     MASK OUT UNSUITABLE CHARACTERS FOR KEY-TYPE TRANSLATIONS.
      CALL CHKDK (IW(IKSTAT), IW(IIMC), NC)
      IF (ITRAN.EQ.7)  CALL CHKDE (IW(IITYPC), IW(IIMC), IW(IIVCON), NC)
C--
 8000 JDIR(ICPH) = 1
      RETURN
C
C--   ERRORS.
 9000 CALL MESSA (4, 1, 0)
      NCERR = NCERR - 1
      CALL EXTERM
      END
      SUBROUTINE CHKDK (KSTAT, IMC, NC)                                     CHKD
 
C* REVISED 25-MAR-93.
C* MASKS OUT CHARACTERS UNSUITABLE FOR KEY-TYPE TRANSLATIONS.
 
C  KSTAT RECEIVES THE NUMBERS OF KEY STATES.
C  IMC RECEIVES AND RETURNS THE CHARACTER MASK.
C  NC RECEIVES THE NUMBER OF CHARACTERS.
 
      DIMENSION KSTAT(NC), IMC(NC)
 
      COMMON /TRNXXX/ IPCHAR,IPITEM,IPNAME,ITRAN
 
      IF (ITRAN.NE.1.AND.ITRAN.NE.2.AND.ITRAN.NE.5.AND.ITRAN.NE.8.AND.
     *  ITRAN.NE.11.AND.ITRAN.NE.6)  GO TO 100
      DO 50 IC = 1, NC
        IF (IMC(IC).EQ.0)  GOTO 50
        IF (KSTAT(IC).LE.1 .OR.
     *   (ITRAN.EQ.1.AND.KSTAT(IC).GT.20))  THEN
          IMC(IC) = 0
          GOTO 50
        ENDIF
C       HENNIG FORMAT.
        IF (ITRAN.EQ.2.AND.KSTAT(IC).GT.10)  THEN
          IMC(IC) = 0
          CALL MESSB (130, IC, 1, 2, 0)
          GOTO 50
        ENDIF
C       PAUP FORMAT.
        IF (ITRAN.EQ.11.AND.KSTAT(IC).GT.32)  THEN
          IMC(IC) = 0
          CALL MESSB (5, IC, 1, 2, 0)
          GOTO 50
        ENDIF
C       NEXUS FORMAT.
        IF (ITRAN.EQ.6.AND.KSTAT(IC).GT.32)  THEN
          IMC(IC) = 0
          CALL MESSB (5, IC, 1, 2, 0)
          GOTO 50
        ENDIF
   50 CONTINUE
  100 RETURN
      END
      SUBROUTINE INISUM (ISUMM, ISBND, LIDAT, NSTAT, KSTAT, ITYPC, NC)      CHKD
C
C* REVISED 6/7/87.
C  INITIALIZES ACCUMULATORS FOR SUMMARY STATISTICS.
C
C  SUMMARY STATISTICS ARE STORED IN THE ARRAY ISUMM. THE FIRST NC
C  LOCATIONS OF ISUMM ARE POINTERS TO THE VALUES FOR EACH CHARACTER.
C  FOR EACH MULTISTATE CHARACTER, 4+NSTAT(IC)+[KSTAT(IC)+1] WORDS ARE
C  ALLOCATED.
C  WORD 1 ACCUMULATES THE NUMBER OF UNCODED OR UNKNOWN ITEMS,
C  WORD 2 THE NUMBER OF INAPPLICABLES,
C  WORD 3 THE NUMBER OF ITEMS CODED WITH A STATE VALUE,
C  WORD 4 THE NUMBER OF VARIABLE ITEMS, AND  THE NEXT NSTAT(IC) WORDS THE
C  NUMBER OF OCCURRENCES OF EACH STATE.
C  IF KEY STATES HAVE BEEN DEFINED, THE NEXT WORD CONTAINS THE
C  NUMBER OF VARIABLE ITEMS IN THE KEY STATES, AND THE NEXT KSTAT(IC)
C  WORDS THE NUMBER OF OCCURRENCES OF EACH KEY STATE.
C  FOR EACH NUMERIC CHARACTER, KSTAT(IC)+10 WORDS ARE ALLOCATED.
C  WORD 1 ACCUMULATES THE NUMBER OF UNCODED OR UNKNOWN ITEMS,
C  WORD 2 THE NUMBER OF INAPPLICABLES,
C  WORD 3 THE NUMBER OF ITEMS RECORDING A NUMERIC VALUE,
C  WORD 4 STORES THE NUMBER OF THE ITEM HAVING THE
C  MINIMUM VALUE OF THE NUMERIC AND WORD 5 THE NUMBER OF THE ITEM
C  HAVING THE MAXIMUM VALUE OF THE NUMERIC. THIS IS FOLLOWED BY 4 WORDS
C  CONTAINING THE MINIMUM VALUE OF
C  THE CHARACTER, THE MAXIMUM VALUE OF THE CHARACTER, THE SUM OF THE
C  CHARACTER VALUES, AND THE SUM OF SQUARES OF CHARACTER VALUES.
C  THE LATTER TWO VALUES WILL BE USED TO CALCULATE MEAN AND STANDARD
C  DEVIATION. IF KEY STATES ARE DEFINED, THE NEXT LOCATION CONTAINS THE
C  NUMBER OF VARIABLE ITEMS, AND THE NEXT KSTAT(IC) WORDS STORE THE NUMBER
C  OF OCCURRENCES OF EACH KEY STATE.
C
C  ISUMM RECEIVES AND RETURNS THE SUMMARY STATISTICS.
C  ISBND RECEIVES THE KEY STATE BOUNDARIES.
C  LIDAT RECEIVES THE LENGTH OF ISUMM, ISBND.
C  NSTAT RECEIVES THE NUMBER OF STATES.
C  KSTAT RECEIVES THE NUMBER OF KEY STATES.
C  ITYPC RECEIVES THE CHARACTER TYPES.
C  NC RECEIVES THE NUMBER OF CHARACTERS.
C
      DIMENSION ISUMM(LIDAT),ISBND(LIDAT),NSTAT(NC),KSTAT(NC),ITYPC(NC)
C
C
      DO 100 IC = 1, NC
        ISUMM(IC) = 0
  100   CONTINUE
C
C     LAST USED WORD IN ISUMM.
      JISUM = NC
C
      DO 300 IC = 1, NC
        JTYPC = IABS(ITYPC(IC))
        IF (JTYPC.EQ.5)  GO TO 110
        IF (JTYPC.GT.2)  GO TO 200
        GO TO 120
C
C       TEXT.
  110   NW = 1
        GO TO 150
C
C       MULTISTATE.
  120   NW = 4 + NSTAT(IC)
        IF (ISBND(IC).NE.0)  NW = NW + KSTAT(IC) + 1
  150   ISUMM(IC) = JISUM + 1
        CALL SETIA (ISUMM(JISUM+1), NW, 0)
        GO TO 290
C
C       NUMERIC.
  200   NW = 9
        IF (ISBND(IC).NE.0)  NW = NW + KSTAT(IC) + 1
        ISUMM(IC) = JISUM + 1
        CALL SETIA (ISUMM(JISUM+1), NW, 0)
C
C       INITIALIZE MINIMUM.
        KISUM = JISUM + 6
        CALL RELOUT (1.E6, ISUMM(KISUM))
        KISUM = KISUM + 1
C
C       ZERO REAL LOCATIONS
        DO 210 J = 1, 3
          CALL RELOUT (0., ISUMM(KISUM))
          KISUM = KISUM + 1
  210     CONTINUE
C
  290   JISUM = JISUM + NW
C
  300   CONTINUE
C
      RETURN
      END
